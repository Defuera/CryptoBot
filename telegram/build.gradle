version '0.1-SNAPSHOT'

apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'de.fuerstenau.buildconfig'

buildscript {
    project.group = 'ru.justd.cryptobot.telegram'
}

buildConfig {

    packageName = project.group

    Properties properties = new Properties()
    def file = project.rootProject.file('./telegram/keystore.properties')
    if (file.exists()) {
        properties.load(file.newDataInputStream())
    } else {
        //parameters so tests could run on ci
        properties.put("BOT_TOKEN", "ci_tests_token")
        properties.put("BOT_NAME", "ci_tests_bot_name")
        properties.put("IS_DEBUG", "true")
        properties.put("FEEDBACK_CHANNEL_ID", "")
    }

    properties.each { key, value ->
        def fieldName = key.toString()
        def fieldValue = value.toString()

        if (fieldName == "IS_DEBUG") {
            buildConfigField 'boolean', fieldName, fieldValue
        } else {
            buildConfigField 'String', fieldName, fieldValue
        }

    }

}

kotlin.experimental.coroutines 'enable'

sourceSets {
    main.kotlin.srcDirs += "$buildDir/generated/source/kapt/main"
}

dependencies {
    implementation project(":core")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"

    implementation 'com.github.pengrad:java-telegram-bot-api:3.3.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.github.hotchemi:khronos:0.9.0' //date extension http://hotchemi.github.io/khronos/
}

//tests
dependencies {
    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation("org.junit.platform:junit-platform-runner:1.0.0")
    testImplementation 'org.mockito:mockito-core:2.7.22'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

run {
    mainClassName = 'ru.justd.cryptobot.MainKt'
}