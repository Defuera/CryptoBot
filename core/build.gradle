version '0.1'

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.fuerstenau.buildconfig'

buildConfig {

    packageName = project.group
    clsName = 'CoreConfig'

    Properties properties = new Properties()
    def file = project.rootProject.file('./core/keystore.properties')
    if (file.exists()) {
        properties.load(file.newDataInputStream())
    } else {
        //parameters so tests could run on ci
        properties.put("KEEN_PROJECT_ID", "")
        properties.put("KEEN_WRITE_KEY", "")
    }

    properties.each { key, value ->
        def fieldName = key.toString()
        def fieldValue = value.toString()
        buildConfigField 'String', fieldName, fieldValue
    }

}

kotlin.experimental.coroutines 'enable'

repositories {
    maven { url = 'https://raw.githubusercontent.com/blockchain/api-v1-client-java/mvn-repo' }
}

sourceSets {
    main.kotlin.srcDirs += "$buildDir/generated/source/kapt/main"
}

dependencies {
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    implementation 'org.mongodb:mongodb-driver:3.5.0'

    implementation 'com.github.hotchemi:khronos:0.9.0' //date extension http://hotchemi.github.io/khronos/
    implementation "io.reactivex.rxjava2:rxjava:2.1.5"

    //dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    //analytics
    compile 'io.keen:keen-client-api-java:5.2.0'

}

//tests
dependencies {
    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation("org.junit.platform:junit-platform-runner:1.0.0")
    testImplementation 'org.mockito:mockito-core:2.7.22'
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"

    //dagger
    testImplementation "com.google.dagger:dagger:$daggerVersion"
    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
